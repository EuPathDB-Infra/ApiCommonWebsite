#!/usr/bin/perl

use lib "$ENV{GUS_HOME}/lib/perl";

use strict;
use DBI;
use File::Basename;
use ApiCommonWebsite::Model::ModelConfig;

usage() unless scalar(@ARGV) >= 6;

my ($geneResultSql, $pValueCutoff, $outputFile, $subOntology, $modelName, @goAssociationSources) = @ARGV;

die "First argument must be an SQL select statement that returns the Gene result\n" unless $geneResultSql =~ m/^select/i;
die "Second argument must be an e-value exponent (negative number)\n" unless $pValueCutoff =~ /-\d+/;

my $c = new ApiCommonWebsite::Model::ModelConfig($modelName);

my $dbh = DBI->connect($c->getAppDb->getDbiDsn, $c->getAppDb->getLogin, $c->getAppDb->getPassword) or die DBI::errstr;

# convert list of sources to oracle-compatible string
my @s = map { "'$_'"} @goAssociationSources;
my $sources = join(",", @s);

my $taxonId = getTaxonId();

getEnrichment($dbh, $taxonId, $sources, $geneResultSql);

##############################################################################

sub getEnrichment {
  my ($dbh, $taxonId, $sources, $geneResultSql) = @_;

  my $annotatedGenesBgd = getAnnotatedGenesCountBgd($dbh, $taxonId, $sources);
  my $annotatedGenesResult = getAnnotatedGenesCountResult($dbh, $sources, $geneResultSql);

  # get query to get back table to feed to python.
  # the columns are:  goId, bgdGeneCount, resultSetGeneCount
  my $dataSql = getDataSql($taxonId, $sources, $geneResultSql);

  my $cmd = "enrichmentAnalysis $pValueCutoff $annotatedGenesBgd $annotatedGenesResult > $outputFile";

  #print STDERR "\n$cmd\n";

  open(E, "| $cmd ") || die "Can't open call to enrichmentAnalysis program: $!\n";

  my $stmt = runSql($dbh, $dataSql);
  while ( my($goId, $geneCountBgd, $geneCountResult, $pctOfBgd, $goName) = $stmt->fetchrow_array()) {
    print E "$goId\t$geneCountBgd\t$geneCountResult\t$pctOfBgd\t$goName\n";
  }
  close E;
}

sub getTaxonId {
  my $sql = "
SELECT distinct ga.taxon_id
FROM  ApidbTuning.GeneAttributes ga,
     ($geneResultSql) r
where ga.source_id = r.source_id
";

  my $stmt = runSql($dbh, $sql);
  my $count = 0;
  my $taxonId;
  while (my ($taxId) = $stmt->fetchrow_array()) { $taxonId = $taxId; $count++; }
  die "Result has genes from more than one taxon\n" if $count != 1;
  return $taxonId;
}

sub getAnnotatedGenesCountBgd {
  my ($dbh, $taxonId, $sources) = @_;

  my $sql = "
SELECT count(distinct ga.source_id)
FROM ApidbTuning.GoTermSummary gts, ApidbTuning.GeneAttributes ga
where ga.taxon_id = $taxonId
  and gts.source_id = ga.source_id
  and gts.is_not is null
  and gts.source in ($sources)
";

  my $stmt = runSql($dbh, $sql);
  my ($geneCount) = $stmt->fetchrow_array();
  die "Got null gene count for bgd annotated genes count\n" unless $geneCount;
  return $geneCount;
}

sub getAnnotatedGenesCountResult {
  my ($dbh, $sources, $geneResultSql) = @_;

  my $sql = "
SELECT count(distinct gts.source_id)
FROM ApidbTuning.GoTermSummary gts,
     ($geneResultSql) r
where gts.source_id = r.source_id
  and gts.is_not is null
  and gts.source in ($sources)
";

  my $stmt = runSql($dbh, $sql);
  my ($geneCount) = $stmt->fetchrow_array();
  die "Got null gene count for result annotated genes count\n" unless $geneCount;
  return $geneCount;
}

sub getDataSql {
  my ($taxonId, $source, $geneResultSql) = @_;

return "
select distinct bgd.go_id, bgdcnt, resultcnt, round(100*resultcnt/bgdcnt, 1) as pct_of_bgd, bgd.name
from
 (SELECT gt.go_id, count(distinct gf.source_id) as bgdcnt, gt.name
            FROM ApidbTuning.GeneAttributes gf,
                 ApidbTuning.GoTermSummary gts,
                 dots.GoAssociation ga, sres.GoTerm gt,
                 sres.GoRelationship gr, core.TableInfo ti,
                 dots.GoAssociationInstance gai,
                 dots.GoAssociationInstanceLoe gail,
                 sres.GoRelationshipType grt
            WHERE gf.taxon_id = $taxonId
              AND gts.ontology = '$subOntology'
              AND ga.go_term_id = gts.go_term_id
              AND ga.row_id = gf.aa_sequence_id
              AND ti.table_id = ga.table_id
              AND ti.name = 'TranslatedAASequence'
              AND ga.is_not = 0
              AND gr.child_term_id = ga.go_term_id
              AND gt.go_term_id = gr.parent_term_id
              AND grt.go_relationship_type_id = gr.go_relationship_type_id
              AND grt.name = 'closure'
              AND gai.go_association_id = ga.go_association_id
              AND gail.go_assoc_inst_loe_id = gai.go_assoc_inst_loe_id
              AND gail.name in ($sources)
            group BY gt.go_id, gt.name
   ) bgd,
   (SELECT gt.go_id, count(distinct gf.source_id) as resultcnt
            FROM ApidbTuning.GeneAttributes gf,
                 ApidbTuning.GoTermSummary gts,
                 dots.GoAssociation ga, sres.GoTerm gt,
                 sres.GoRelationship gr, core.TableInfo ti,
                 sres.GoRelationshipType grt,
                 dots.GoAssociationInstance gai,
                 dots.GoAssociationInstanceLoe gail,
                 ($geneResultSql) r
            WHERE gf.aa_sequence_id = ga.row_id
              AND gts.ontology = '$subOntology'
              AND ga.go_term_id = gts.go_term_id
              AND ga.table_id = ti.table_id
              and gf.source_id = r.source_id
              AND ti.name = 'TranslatedAASequence'
              AND ga.is_not = 0
              AND ga.go_term_id = gr.child_term_id
              AND gr.parent_term_id = gt.go_term_id
              AND gr.go_relationship_type_id = grt.go_relationship_type_id
              AND grt.name = 'closure'
              AND ga.go_association_id = gai.go_association_id
              AND gai.go_assoc_inst_loe_id = gail.go_assoc_inst_loe_id
              AND gail.name in ($sources)
            group BY gt.go_id
      ) rslt
where bgd.go_id = rslt.go_id
";
}

sub runSql {
  my ($dbh, $sql) = @_;
  # print STDERR "\n$sql\n\n";
  my $stmt = $dbh->prepare("$sql") or die(DBI::errstr);
  $stmt->execute() or die(DBI::errstr);
  return $stmt;
}

sub usage {
  my $this = basename($0);

  die "
Find GO terms that are enriched in the provided set of Genes.

Usage: $this sqlToFindGeneList pValueCutoff  outputFile subOntologyName modelName annotationSources

Where:
  sqlToFindGeneList:    a select statement that will return all the rows in the db containing the genes result. Must have a source_id column.
  pValueCutoff:         the p-value exponent to use as a cutoff.  terms with a larger exponent are not returned
  outputFile:           the file in which to write results
  subOntologyName:      'Molecular Function' etc
  modelName:            eg, PlasmoDB.  Used to find the database connection.
  annotationSources:    only include annotation that comes from these one or more sources.  (Eg, GeneDB, InterproScan).

The gene result must only include genes from a single taxon.  It is an error otherwise.

The output file is tab-delimited, with these columns (sorted by e-value)
  - e-value
  - GO ID
  - number of genes in organism with this term
  - number of genes in result with this term
  - GO TERM

";

}
